plugins {
    id 'com.android.application'
    id 'com.jakewharton.butterknife'
}

repositories {
    google()
    maven {
        url "file://${projectDir.absolutePath}/../../../../../build/localMaven"
    }
    mavenCentral()
}

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        // This is different than the manifest.
        applicationId 'com.example.butterknife'
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1
        versionName '1.0.0'
    }

    // Add differing applicationIdSuffixes for debug and release to ensure that the gradle plugin
    // finds the R.java file correctly.
    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
        }
        release {
            applicationIdSuffix = ".release"
        }
    }

    flavorDimensions "flavorA"

    // Override the applicationId in flavors to ensure that the gradle plugin
    // finds the R.java file correctly.
    productFlavors {
        flavorA {
            applicationId "foo.bar"
        }

        flavorB {
            applicationId "bar.foo"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

}

dependencies {
    implementation "androidx.core:core:1.0.0"
    implementation "com.jakewharton:butterknife:${getProperty("butterknife.version")}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${getProperty("butterknife.version")}"
}
